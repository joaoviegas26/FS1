#include <Arduino.h>
#include <FlexCAN.h>
#include <elapsedMillis.h>

#define SENSITIVE 0.0057 //sensibilidade do sensor (0.0057 mV/A)
#define Uc 5 //suply voltage (5 V)
#define Vo 2.5 //offset voltage (2.5 V)
#define R1 4.7 //resitencia R1 (4.7 kOhm)
#define R2 78 //resitencia R2 (78 kOhm)

#define CURRENT_PIN A4 //pin de entrada do sinal que provem do sensor de corrente
#define ID 0x5f //ID da mensagem CAN

#define CAN_RATE 500000 //taxa de transmissão (500 kbps)
#define TRANSMISSION_PERIOD 8 //período de envio da corrente (8 ms)

FlexCAN_T4<CAN1, RX_SIZE_256, TX_SIZE_16> can1;

CAN_message_t currentMessage;
elapsedMillis currentTimer;

void canSetup(){
    can1.begin(); //inicializa a comunicação CAN
    can1.setBaudRate(CAN_RATE); //taxa de transmissão de 500kbps

    currentMessage.id = ID; //define o ID da mensagem
    currentMessage.len = 2; //define o tamanho/comprimento da mensagem em bytes

    can1.enableFIFO(); //FIFO(First In First Out), armazena mensagens lidas pela ordem que chegaram
    can1.enableFIFOInterrupt(); //micro-controlador é notificado quando uma nova mensagem é recebida
}

void sendCurrent(float current){
    int16_t currentValue = static_cast<int16_t>(current * 100); //conversão de um valor float para um tipo int16_t, inteiro de 16 bits. 
    //multiplicar por 100 tem o objetivo de conservar 2 casas decimais
    //o destinatário da mensagem terá que dividir o valor recebido por 100
 
    currentMessage.buf[0] = (currentValue >> 8) & 0xFF; // MSB, byte mais significativo
    currentMessage.buf[1] = currentValue & 0xFF; // LSB, byte menos significativo
    
    can1.write(currentMessage); //enviar a mensagem CAN
}

void setup(){
    canSetup(); //configurar a CAN
}

void loop(){
    if (currentTimer > TRANSMISSION_PERIOD){
        currentTimer = 0; //reiniciar o temporizador

        float Vout_read = analogRead(CURRENT_PIN); //leitura do valore de saída do sensor de corrente atenuado pelo divisor de tensão
        float Vout_conv = Vout_read*(3.3/1023); //conversão do valor lido, já que analogRead() retorna um valor entre 0 e 1023, que representa os 0V até 3.3V

        float Vout = Vout_conv*((R1+R2)/R2); //valor de tensão real
        float I = (Vout*(5/Uc)-Vo)/SENSITIVE; //calcular corrente

        sendCurrent(I); //enviar o valor da corrente I para a CAN
    }
}